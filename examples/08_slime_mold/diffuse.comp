#version 450
#extension GL_EXT_nonuniform_qualifier : require

// Binding 1: Read (Old Frame)
layout(set = 0, binding = 1) uniform sampler2D all_textures[];

// Binding 2: Write (New Frame)
layout(set = 0, binding = 2, rgba8) uniform image2D all_storage_images[];

layout(push_constant) uniform PushConstants {
    float dt;
    uint  read_tex_id;
    uint  write_img_id;
} pc;

layout (local_size_x = 16, local_size_y = 16) in;

void main() {
    ivec2 pos = ivec2(gl_GlobalInvocationID.xy);
    ivec2 size = imageSize(all_storage_images[pc.write_img_id]);
    
    if (pos.x >= size.x || pos.y >= size.y) return;

    // 3x3 Blur
    vec4 sum = vec4(0.0);
    for (int y = -1; y <= 1; y++) {
        for (int x = -1; x <= 1; x++) {
            // Read from texture sampler (normalized coords)
            vec2 uv = (vec2(pos + ivec2(x, y)) + 0.5) / vec2(size);
            sum += texture(all_textures[pc.read_tex_id], uv);
        }
    }
    vec4 blurred = sum / 9.0;

    // Decay
    float decay_rate = 0.5 * pc.dt;
    vec4 final_color = max(vec4(0.0), blurred - decay_rate);
    
    // Keep alpha 1 for rendering
    final_color.a = 1.0;

    imageStore(all_storage_images[pc.write_img_id], pos, final_color);
}
